!  
!   
!                             MLD2P4  version 2.2
!    MultiLevel Domain Decomposition Parallel Preconditioners Package
!               based on PSBLAS (Parallel Sparse BLAS version 3.5)
!    
!    (C) Copyright 2008-2018 
!  
!        Salvatore Filippone  
!        Pasqua D'Ambra   
!        Daniela di Serafino   
!   
!    Redistribution and use in source and binary forms, with or without
!    modification, are permitted provided that the following conditions
!    are met:
!      1. Redistributions of source code must retain the above copyright
!         notice, this list of conditions and the following disclaimer.
!      2. Redistributions in binary form must reproduce the above copyright
!         notice, this list of conditions, and the following disclaimer in the
!         documentation and/or other materials provided with the distribution.
!      3. The name of the MLD2P4 group or the names of its contributors may
!         not be used to endorse or promote products derived from this
!         software without specific written permission.
!   
!    THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
!    ``AS IS'' AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
!    TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
!    PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE MLD2P4 GROUP OR ITS CONTRIBUTORS
!    BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
!    CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
!    SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
!    INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
!    CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
!    ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
!    POSSIBILITY OF SUCH DAMAGE.
!   
!  
subroutine mld_s_gs_solver_bld(a,desc_a,sv,info,b,amold,vmold,imold)

  use psb_base_mod
  use mld_s_gs_solver, mld_protect_name => mld_s_gs_solver_bld

  Implicit None

  ! Arguments
  type(psb_sspmat_type), intent(in), target           :: a
  Type(psb_desc_type), Intent(inout)                  :: desc_a 
  class(mld_s_gs_solver_type), intent(inout)         :: sv
  integer(psb_ipk_), intent(out)                      :: info
  type(psb_sspmat_type), intent(in), target, optional :: b
  class(psb_s_base_sparse_mat), intent(in), optional  :: amold
  class(psb_s_base_vect_type), intent(in), optional   :: vmold
  class(psb_i_base_vect_type), intent(in), optional   :: imold
  ! Local variables
  integer(psb_ipk_) :: n_row,n_col, nrow_a, nztota
  integer(psb_ipk_) :: ictxt,np,me,i, err_act, debug_unit, debug_level
  character(len=20) :: name='s_gs_solver_bld', ch_err

  info=psb_success_
  call psb_erractionsave(err_act)
  debug_unit  = psb_get_debug_unit()
  debug_level = psb_get_debug_level()
  ictxt       = desc_a%get_context()
  call psb_info(ictxt, me, np)
  if (debug_level >= psb_debug_outer_) &
       & write(debug_unit,*) me,' ',trim(name),' start'


  n_row  = desc_a%get_local_rows()

  nrow_a = a%get_nrows()
  nztota = a%get_nzeros()

  if (sv%eps <= dzero) then
    !
    ! This cuts out the off-diagonal part, because it's supposed to
    ! be handled by the outer Jacobi smoother.
    ! 
    call a%tril(sv%l,info,diag=izero,jmax=nrow_a,u=sv%u)
    !
    ! Is this an L1-GS solver? 
    !
    if (allocated(sv%xtra)) then
      block
        integer(psb_ipk_) :: k, nz, nrm, dp
        type(psb_s_coo_sparse_mat) :: tcoo
        !
        ! For GS:  LX = L + D, UX = U - D
        !
        call sv%l%mv_to(tcoo)
        nrm = min(psb_size(sv%xtra),tcoo%get_nrows(),tcoo%get_ncols())
        nz = tcoo%get_nzeros()
        call tcoo%ensure_size(nz+nrm)
        call tcoo%set_dupl(psb_dupl_add_)
        do k=1,nrm
          if (sv%xtra(k) /= szero) then
            nz = nz + 1
            tcoo%ia(nz)  = k
            tcoo%ja(nz)  = k
            tcoo%val(nz) = sv%xtra(k)
          end if
        end do
        call tcoo%set_nzeros(nz)
        call tcoo%fix(info)
        call sv%l%mv_from(tcoo)

        call sv%u%mv_to(tcoo)
        nrm = min(psb_size(sv%xtra),tcoo%get_nrows(),tcoo%get_ncols())
        nz = tcoo%get_nzeros()
        call tcoo%ensure_size(nz+nrm)
        call tcoo%set_dupl(psb_dupl_add_)
        do k=1,nrm
          if (sv%xtra(k) /= szero) then
            nz = nz + 1
            tcoo%ia(nz)  = k
            tcoo%ja(nz)  = k
            tcoo%val(nz) = -sv%xtra(k)
          end if
        end do
        call tcoo%set_nzeros(nz)
        call tcoo%fix(info)
        call sv%u%mv_from(tcoo)        
      end block
    end if

  else

    info = psb_err_missing_override_method_
    call psb_errpush(info,name)
    goto 9999       
  end if



  call sv%l%set_asb()
  call sv%l%trim()
  call sv%u%set_asb()
  call sv%u%trim()

  if (present(amold)) then 
    call sv%l%cscnv(info,mold=amold)
    call sv%u%cscnv(info,mold=amold)
  end if

  if (debug_level >= psb_debug_outer_) &
       & write(debug_unit,*) me,' ',trim(name),' end'

  call psb_erractionrestore(err_act)
  return

9999 call psb_error_handler(err_act)

  return
end subroutine mld_s_gs_solver_bld
